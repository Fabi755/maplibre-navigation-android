android.libraryVariants.all { variant ->
  def name = variant.name
  task "javadoc$name"(type: Javadoc) {
    description = "Generates javadoc for build $name"
    failOnError = false
    destinationDir = new File(destinationDir, variant.baseName)
    source = files(variant.javaCompile.source)
    doFirst {
      classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath) + configurations.javadocDeps
    }
    options.windowTitle("MapLibre Android Navigation SDK ${project.ext.versionName} Reference")
    options.docTitle("MapLibre Android Navigation SDK ${project.ext.versionName}")
    options.header("MapLibre Android Navigation SDK ${project.ext.versionName} Reference")
    options.linksOffline("http://d.android.com/reference/", "$System.env.ANDROID_HOME/docs/reference")
    exclude '**/R.java', '**/BuildConfig.java'
  }
}